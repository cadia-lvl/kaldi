# Expand ordinals
#
# Copyright 2017  Reykjavik University (Author: Inga Rún Helgadóttir)
# Apache 2.0

import 'local/common.grm' as c;
import 'local/numbers.grm' as n;

insspace = "".utf8 : " ".utf8;

delzero = "0".utf8 : "".utf8;
delone  = "1".utf8 : "".utf8;
delperiod = ".".utf8 : "".utf8;
insand = "".utf8 : " og ".utf8;

ordUnits = Optimize[StringFile['text_norm/lex/ordinals_units_lexicon.txt',
'utf8', 'utf8']];

ordTeens = Optimize[StringFile['text_norm/lex/ordinals_teens_lexicon.txt', 'utf8', 'utf8']];

ordDecades = Optimize[StringFile['text_norm/lex/ordinals_decades_lexicon.txt', 'utf8', 'utf8']];

ordinals_20_to_99 = ordDecades delzero delperiod | (ordDecades insand
ordUnits <-3>);

hundreds1 = ((((("eitt".utf8 <2.0>) insspace) | delone)
  ("".utf8 : ("hundraðasta".utf8 | "hundraðasti".utf8)))
  | (n.neutral_units2to9 insspace
  ("".utf8 : ("hundraðasti".utf8 | "hundraðasta".utf8 | "hundruðustu".utf8))));

hundreds2 = ((((("eitt".utf8 <2.0>) insspace) | delone)
  ("".utf8 : "hundrað".utf8))
  | (n.neutral_units2to9 insspace
  ("".utf8 : "hundruð".utf8)));

ordinals_100_to_999 = Optimize[
  hundreds1 delzero delzero delperiod |
  (hundreds2 delzero insand ordUnits <-1>) |
  (hundreds2 insand ordTeens <-2>) |
  (hundreds2 (insspace | insand) ordinals_20_to_99 <-10>)]
;

ordinals = Optimize[
  (ordUnits <-0.5>) |
  (ordTeens <-1>) |
  (ordinals_20_to_99 <-2>) |
  (ordinals_100_to_999 <-10>)]
;

#export ORDINALNAMES = Optimize[ordinals @ n.add_and];
export ORDINALNAMES = ordinals;

ordUnits3to9 = Optimize[StringFile['text_norm/lex/ordinals_units_3-9.txt',
'utf8', 'utf8']];
export ORDINALNAMES2 = Optimize[
  (ordUnits3to9 <-0.5>) |
  (ordTeens <-1>) |
  (ordinals_20_to_99 <-2>) |
  (ordinals_100_to_999 <-10>)]
;
